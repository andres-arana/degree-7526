[Subsistema nuevo]
*Definición de funciones
tipo		FUNCTION	RN2, D4
	0.2, 1 / 0.5, 2 / 0.9, 3 / 1, 4
bar		FUNCTION	P$tipo, D4
	1, 500 / 2, 100 / 3, 750 / 4, 450
tMuseo	FUNCTION	P$tipo, D4
	1, 3600 / 2, 5400 / 3, 1800 / 4, 2400
tBar		FUNCTION	P$tipo, D4
	1, 900 / 2, 600 / 3, 1500 / 4, 1200
*emulo la cantidad equiprobable entre 6 a 10
exc1		VARIABLE	6+RN3@5
*emulo la cantidad equiprobable entre 20 a 40
exc2		VARIABLE	20+RN4@21
*estacionamiento para 5 buses pequeños
estac		STORAGE	5
* cajashabilitadas
INITIAL	X1, 2
* caja elegida al azar
cAzar		VARIABLE	1+RN5@X1
* storage para el espacio alojado del bar
bar		STORAGE		100000

* Tiempo en cola de la estacion de servicio
estserv		QTABLE		estserv,0,60,20
* Tiempo en cola en el estacionamiento
estac		QTABLE		estac,0,60,20
* Tiempo total del bus que estuvo en el sistema
tsist		TABLE		M1,3600,600,20
* El tiempo que los excursionistas estuvieron en el museo
tmuseo		TABLE		MP1,600,600,20
* Cantidad de personas que ingresaron en un bar
cantbar		TABLE		S$bar,0,5,20

* armo subsistema de cajas habilitadas
		GENERATE	,,7200,1
		SAVEVALUE	X1, 3
		ADVANCE		3600
		SAVEVALUE	X1, 5
		TERMINATE

*Subsistema para contar la cantidad de gente en el bar
		GENERATE	120,0,3600
		TABULATE	cantbar
		TERMINATE

*Llegan pequeños buses de excursión
		GENERATE	900, 600, , 25
		ASSIGN		ocupa, 1	; un bus pequeño ocupa 1 lugar
		ASSIGN		exc, V$exc1
		TRANSFER	, ESTAC
*Llegan buses de excursión
		GENERATE	600, 300, 7200, 35
		ASSIGN		ocupa, 2	; un bus grande ocupa 2 lugares
		ASSIGN		exc, V$exc2
		TRANSFER	, ESTAC
ESTAC		QUEUE		estac
		ENTER		estac, P$ocupa
		DEPART		estac
		ADVANCE		90, 30		; demoran en estacionar
		ADVANCE		60, 30		; demoran en hacer bajar a los excursionistas
		SPLIT		P$exc, REUNION	; los excursionistas bajan todos juntos
		ADVANCE		20, 10		; demoran en salir del estacionamiento
		LEAVE		estac, P$ocupa
		ADVANCE		300, 60		; se demoran en llegar a una estación de servicio
		QUEUE		estserv
		SEIZE		estserv
		DEPART		estserv
		ADVANCE		180, 60		; demoran en atender a 1 bus
		RELEASE		estserv
		ADVANCE		600, 120
SUBE		ASSEMBLE	2		; mato a todos y los transformo en el bondi
		ADVANCE		900, 300
		TABULATE	tsist
		TERMINATE	1		; mato al bondi
REUNION		ASSIGN		tipo, FN$tipo
		ADVANCE		300, 120
		MARK		1
		GATHER		P$exc		; dice una vez reunido todo el grupo
		QUEUE		PUERTA
		SEIZE		PUERTA		; PARA INGRESO DE CADA PERSONA
		DEPART		PUERTA
		ADVANCE		4, 2
		RELEASE		PUERTA
		ADVANCE 	FN$tMuseo, 1200	; tiempo que permanece en el museo según el tipo
		TABULATE	tmuseo
		TRANSFER	FN$bar, NO_BAR, BAR
BAR		QUEUE		bar
		ENTER		bar
		DEPART		bar
		ADVANCE 	FN$tBar, 300	; tiempo que permanece en el bar según el tipo
		TRANSFER	.3, , AZAR
		SELECT NU	caja,1,X1,,,OCUP ; el 70% elije la caja 
		TRANSFER	, CAJA
OCUP		SELECT MIN	caja,1,X1,,Q	; en caso de estar todas ocupadas la que tiene cola minima
		TRANSFER	, CAJA
AZAR		ASSIGN		caja, V$cAzar
CAJA		QUEUE		P$caja
		SEIZE		P$caja
		DEPART		P$caja
		ADVANCE		60, 30		; demora adjudicada al pago
		RELEASE		P$caja
		LEAVE		bar
		ADVANCE		180, 120
		TRANSFER	, BAR_OUT
NO_BAR		ADVANCE		1800, 900	; los que no van al bar, pasean
BAR_OUT		ASSEMBLE	P$exc		; reuno al grupo
		ADVANCE		90, 30
		TRANSFER	, SUBE


<\F>
Nombre=Grupo2
Descripcion=Trabajo Practico - Buses de excursionistas
Version=1.0
Autor=Andres Arana, Sergio Piano
Empresa=FIUBA
Opciones=
